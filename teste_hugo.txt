#<AdxTL>@(#)0.0.0.0 $Revision$
$PROFORMA
Gosub OPEN_LNK_PFO From SAFTGENPORWD

For [WDT3]
  Gosub RAZVAR_WD From SAFTGENPORWD
  Call LEC_TSV_LEG([L]LEG, [F:PTSIH]SIVTYP, STAT) From TRTLECLEG
  [L]WDRATCUR = arr([F:PTSIH]RATMLT([L]LEDT-1)/[F:PTSIH]RATDIV([L]LEDT-1),10^-GDECRCU)
  If [L]WDRATCUR = 0 : [L]WDRATCUR = 1 : Endif
  Gosub FEED_VARS
Next

Gosub CLOSE_LNK_PFO From SAFTGENPORWD

Return
#######################################################################################################
#######################################################################################################
$FEED_VARS

Read [F:DKD]DKD0 = "SAFT-PT";[F:PTSIV]SIVTYP;[F:PTSIV]SIHNUMEND
If fstat : Raz [F:DKD] : Endif

#DocumentNumber
[L]WDDOCNUMB = [F:DKD]DKSDOCNUM

#ATCUD
If [F:DKD]ATCUD = ""
  [L]WDATCUD = "0"
Else
  [L]WDATCUD = [F:DKD]ATCUD
Endif

#WorkStatus
[L]WDMOVSTAT = 'N'

#SystemEntryDate
If [F:DKD]RECTYP <> 4
  [L]WDSYSENTD = format$("D:YYYY[-]MM[-]DD",[F:DKD]VALIDDAT) + "T" + [F:DKD]VALIDTIM
Else
  If [F:DKD]DKSDOCNUM = ""
    #[L]SDINVSTADA = format$("D:YYYY[-]MM[-]DD",[F:SIH]UPDDATTIM)+"T" + func SUBADC.TIM_DATETIME([F:SIH]UPDDATTIM)
    If [F:DKD]VALIDTIM <>"" : [L]SDLVALIDTIM = [F:DKD]VALIDTIM : Else : [L]SDLVALIDTIM = "00:00:00" : Endif
    If [F:DKD]VALIDDAT=[0/0/0]
      [L]WDSYSENTD = format$("D:YYYY[-]MM[-]DD",[F:PTSIH]ACCDAT)+"T"+ [L]SDLVALIDTIM #[F:DKD]VALIDTIM
    Else
      [L]WDSYSENTD = format$("D:YYYY[-]MM[-]DD",[F:DKD]VALIDDAT)+"T"+ [L]SDLVALIDTIM #[F:DKD]VALIDTIM
    Endif
  Else
    [L]WDSYSENTD = [F:DKD]SYSENTDAT
  Endif
Endif

#WorkStatusDate
[L]WDMOVSTDT = [L]WDSYSENTD

#SourceID
[L]WDUPDUSER = [F:PTSIH]CREUSR

#SourceBilling
[L]WDSOURCEB = "P"

#Hash
[L]WDHASHCOD = [F:DKD]DIGSIGN # "0"

#HashControl
[L]WDHSHCONT = num$([F:DKD]KEYVER) # "1"

#Period
[L]WDPERIODE  = func SAFTGENPOR.GET_PERIOD_NUMBER([F:PTSIH]CPY, [F:PTSIH]ACCDAT)

#WorkDate
[L]WDMOVEDAT  = format$("D:YYYY[-]MM[-]DD",[F:PTSIH]ACCDAT)

#WorkType
Call LEC_TSV_LEG([L]LEG,[F:PTSIH]SIVTYP,[L]STAT) From TRTLECLEG
If [L]STAT : Raz [F:TSV] : Endif
If [F:TSV]SAFTINVTYP=9
  [L]WDMOVTYPE="FC"
Else
  [L]WDMOVTYPE='PF'
Endif

#SourceID(Creation)
If [L]WDCREUSER = ''
  [L]WDCREUSER = [F:PTSIH]CREUSR
Endif

#CustomerID
[L]WDCUSTOID = [F:PTSIH]BPR

If [L]SAFTWAY=1
 Gosub WRITE_WOKDOC_H From SAFTGENPORWD
Endif

Gosub GET_TAX_ARRAY #Array footer elements

For [F:PTSID] Where [F:PTSID]NUM=[F:PTSIH]NUM
  Gosub RAZVAR_WD_D From SAFTGENPORWD
  #LineNumber
  #[L]WDDLINNBR = [F:PTSID]SIDLIN
  [L]WDDLINNBR += 1

  #ProductCode
  [L]WDDPRODUC = [F:PTSID]ITMREF

  #ProductDescription
  # Issue X3-132889 - 2019-04-05 by RMA : description from itmmaster\atextra for POR language
  Call LECTEXTRA_POR([L]WDDPRODES,"ITMMASTER","DES1AXX", [F:PTSID]ITMREF,"") From SAFTGENPOR

  #Quantity
  [L]WDDQUANTI  = [F:PTSID]QTY

  #Date
  [L]WDLENTDAT = format$("D:YYYY[-]MM[-]DD",[F:PTSID]INVDAT)

  #UnitOfMeasure
  [L]WDDUOM    = [F:PTSID]SAU

  #UnitPrice
  [L]WDDNETPRI = abs([F:PTSID]NETPRINOT * [L]WDRATCUR)

  #Description
  If [F:PTSID]ITMDES1<>""
    [L]WDDLINDES = [F:PTSID]ITMDES1
  Else
    [L]WDDLINDES = "Desconhecido"
  Endif

  #Footer elements
  Call FOOTER_ELEMENT([F:PTSID]VAT(0),ARRAY_VAT,ARRAY_VATNET,DIMSVV,ARRAY_FOOTVAT,ARRAY_FOOTNET,DIMSVF,SAFPRIRND,[L]WDDQUANTI,[L]WDDNETPRI,WDSETAMOUT)

  #DebitAmount/CreditAmount
  If [F:PTSID]NETPRINOT<0
    [L]WDDDEBAMT = abs(arr(([L]WDDQUANTI * [L]WDDNETPRI), [L]SAFPRIRND))
  Else
    [L]WDDCREAMT = abs(arr(([L]WDDQUANTI * [L]WDDNETPRI), [L]SAFPRIRND))
  Endif

  #TaxType,TaxCountryRegion,TaxCode,TaxPercentage,TaxExemptionReason
  [L]WDDTAXTYP = "IVA"
  For YC = 0 To 0  #Alterar?
   If [F:PTSID]VAT(YC)<>""
     Call GETTAX([L]SOC,[F:PTSID]VAT(YC),[F:PTSIH]ACCDAT,[L]WDDTAXTYP,[L]WDDTAXCOU,[L]WDDTAXCOD,[L]WDDTAXPER,[L]WDDTAXAMO,[L]WDDTAXEXA,[L]WDDTAXEXAC,[L]VENEXEFLG) From SAFTGENPORVAT
     [L]ELTAX = [F:PTSID]VAT(YC)
     If [L]VENEXEFLG = 2 : WDDTAXPER = 0.0 : Endif
   Endif
  Next

  # Issue X3-204744 - 2020-10-13 by VAVIL
  Raz [L]SDLPRFNUM,[L]SDLPRFDAT
  Read [F:SDH]SDH0=[F:PTSID]SDHNUM
  If !fstat
    If [F:SDH]PRFNUM<>""
    Read [F:SIH1]SIH0 =[F:SDH]PRFNUM
      If !fstat
        Call LEC_TSV_LEG("POR",[F:SIH1]SIVTYP,STAT) From TRTLECLEG
        If [F:TSV]SAFTINVTYP=9 and !STAT
          Read [F:DKD2]DKD0="SAFT-PT";[F:SDH]SDHTYP;[F:SDH]SDHNUM
          If !fstat
            [L]SDLPRFNUM=[F:DKD2]DKSDOCNUM
          Else
            [L]SDLPRFNUM = [F:PTSID]SDHNUM
          Endif
          If [F:SDH]DLVDAT<[F:SIH1]ACCDAT
            [L]SDLPRFDAT = format$("D:YYYY[-]MM[-]DD",[F:SDH]SHIDAT)
          Else
            [L]SDLPRFDAT = format$("D:YYYY[-]MM[-]DD",[F:SIH1]ACCDAT)
          Endif
        Endif
      Endif
    Endif
  Endif
  # Issue end X3-204744

  If [L]SAFTWAY=1
    Gosub WRITE_WOKDOC_D From SAFTGENPORWD
  Else
    # Issue X3-222040- 2020-11-01 by VAVIL : [PT] QR code printed in Sales documents
    If dim([L]QRCODEWAY ) > 0
      If [L]QRCODEWAY >= 1 : Gosub FEED_VAR_QRC_WD From PORLEGQRCDOCLIB : Endif
    Endif
    # End issue X3-222040
  Endif
Next

For [F:PTSVF] Where [F:PTSVF]VCRNUM=[F:PTSIH]NUM
  Gosub RAZVAR_WD_D From SAFTGENPORWD
  For I=0 To 9
    If (!find([F:PTSVF]DTAVAT(I),[L]CTRLVAT(1..[L]COUNTVAT)) & [F:PTSVF]DTAVATNOT(I) <> 0) | [F:PTSVF]DTAVATNOT(I) > 0
      Read [F:VCL]VCL0=[L]SOC;[F:PTSVF]DTAVAT(I)
      If !fstat
        #[L]WDDLINNBR   += 1000
        [L]WDDLINNBR   += 1
        [L]WDDQUANTI   = 1
        [L]WDDNETPRI   = abs([F:PTSVF]DTAVATNOT(I) * [L]WDRATCUR)
        [L]WDDUOM      = "UN"
        [L]WDDPRODUC   = "ELEMFACT"
        [L]WDDPRODES   = "Elementos de Facturação"
        # Issue X3-99026 by MRDAB
        Call LECTEXTRA_POR([L]WDDLINDES,"SFOOTINV","DESAXX",num$([F:PTSVF]DTA),"") From SAFTGENPOR
        # Issue X3-99026 end
        If [F:PTSVF]DTAVATNOT(I)<0
          [L]WDDDEBAMT=abs([F:PTSVF]DTAVATNOT(I) * [L]WDRATCUR)
        Else
          [L]WDDCREAMT=abs([F:PTSVF]DTAVATNOT(I) * [L]WDRATCUR)
        Endif
        Call GETTAX([L]SOC,[F:PTSVF]DTAVAT(I),[F:PTSIH]ACCDAT,[L]WDDTAXTYP,[L]WDDTAXCOU,[L]WDDTAXCOD,[L]WDDTAXPER,[L]WDDTAXAMO,[L]WDDTAXEXA,[L]WDDTAXEXAC,[L]VENEXEFLG) From SAFTGENPORVAT
        [L]ELTAX = [F:PTSVF]DTAVAT(I)
        If [L]VENEXEFLG = 2 : WDDTAXPER = 0.0 : Endif
        #Date
        [L]WDLENTDAT = format$("D:YYYY[-]MM[-]DD",[F:PTSID]INVDAT)

        [L]WDDDEBAMT = arr([L]WDDDEBAMT, [L]SAFPRIRND)
        [L]WDDCREAMT = arr([L]WDDCREAMT, [L]SAFPRIRND)
        If [L]SAFTWAY=1
          Gosub WRITE_WOKDOC_D From SAFTGENPORWD
        Else
        # Issue X3-222040- 2020-11-01 by VAVIL : [PT] QR code printed in Sales documents
          If dim([L]QRCODEWAY ) > 0
            If [L]QRCODEWAY >= 1 : Gosub FEED_VAR_QRC_WD From PORLEGQRCDOCLIB : Endif
          Endif
          # End issue X3-222040
        Endif
      Endif
    Endif
    Next
Next

#-
#Footer elements
#TaxPayable
[L]WDTTAXPAY = [F:PTSIH]AMTATIL-[F:PTSIH]AMTNOTL
#NetTotal
[L]WDTNETTOT = [F:PTSIH]AMTNOTL
#GrossTotal
[L]WDTGROTOT = [F:PTSIH]AMTATIL
#CurrencyAmount
[L]WDTCURAMT = [F:PTSIH]AMTATI

If [F:PTSIH]CUR <>'EUR'
  #CurrencyCode
  #[L]WDTCURCOD = [F:PTSIH]CUR
  #ExchangeRate
  [L]WDTEXCRAT = [L]WDRATCUR
  If [F:TCU]CUR <> [F:PTSIH]CUR
    Read [F:TCU]TCU0 = [F:PTSIH]CUR
  Endif
  [L]WDTCURCOD = [F:TCU]ISOCOD
Endif

If [L]SAFTWAY=1
 Gosub WRITE_WOKDOC_T From SAFTGENPORWD
Endif

Return
#############################################################################################
#############################################################################################
$GET_TAX_ARRAY
Raz[L]CTRLVAT : [L]COUNTVAT =1
Raz ARRAY_VAT, ARRAY_VATNET, ARRAY_FOOTVAT, ARRAY_FOOTNET,[L]SDL1

#Tax
Filter [F:SVV] Where [F:SVV]VCRNUM = [F:PTSIH]NUM   & [F:SVV]VATTYP = 1 Order By [F:SVV]VAT Asc
For [F:SVV]
  [L]CTRLVAT([L]COUNTVAT) = [F:SVV]VAT  : [L]COUNTVAT + = 1
  ARRAY_VAT([L]SDL1)      = [F:SVV]VAT
  ARRAY_VATNET([L]SDL1)   += [F:SVV]VATNET
  [L]SDL1 += 1
Next
Filter [F:SVV]

#Footer  elements
Raz [L]SDL1
For [F:SVF] Where [F:SVF]VCRNUM=[F:PTSIH]NUM
  #Gosub SET_IS_WITHHOLDINGTAX From DKSLIB
  #If ![L]ISWITHHOLDINGTAX
    For I=0 To 9
      Raz FOOT_RAT, FLG
      If [F:SVF]DTAVATNOT(I) >= 0 : Goto NEXT_TAX   : Endif

      If [F:SVF]AMTCOD = 1 #percentagem
        K = find([F:SVF]DTAVAT(I),ARRAY_FOOTVAT(0..SDL1))
        If K
          ARRAY_FOOTNET(K-1) += abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
        Else
          For K = 0 To SDL1
            If ARRAY_FOOTVAT(K)<>""
              If left$(ARRAY_FOOTVAT(K),len([F:SVF]DTAVAT(I)))=[F:SVF]DTAVAT(I)
                FOOT_RAT = val(seg$(ARRAY_FOOTVAT(K),instr(0,ARRAY_FOOTVAT(K),"~")+1,len(ARRAY_FOOTVAT(K))))
                FOOT_RAT += [F:SVF]DTAAMT
                ARRAY_FOOTVAT(K) = [F:SVF]DTAVAT(I)+"~"+num$(FOOT_RAT)
                ARRAY_FOOTNET(K) += abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
                FLG = 1
                Break
              Endif
            Endif
          Next
          If FLG = 0
            ARRAY_FOOTVAT(SDL1) = [F:SVF]DTAVAT(I)+"~"+num$([F:SVF]DTAAMT)
            ARRAY_FOOTNET(SDL1) = abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
            SDL1+=1
          Endif
        Endif
      Else #montante
        K = find([F:SVF]DTAVAT(I),ARRAY_FOOTVAT(0..SDL1))
        If K
          ARRAY_FOOTNET(K-1) += abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
        Else
          For K = 0 To SDL1
            If ARRAY_FOOTVAT(K)<>""
              If left$(ARRAY_FOOTVAT(K),len([F:SVF]DTAVAT(I)))=[F:SVF]DTAVAT(I)
                ARRAY_FOOTVAT(K) = [F:SVF]DTAVAT(I)
                ARRAY_FOOTNET(K) += abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
                FLG = 1
                Break
              Endif
            Endif
          Next
          If FLG = 0
            ARRAY_FOOTVAT(SDL1) = [F:SVF]DTAVAT(I)
            ARRAY_FOOTNET(SDL1) = abs([F:SVF]DTAVATNOT(I)*[L]WDRATCUR)
            SDL1+=1
          Endif
        Endif
      Endif
     $NEXT_TAX
    Next
  #Endif
Next

Return
######################################################################################
######################################################################################
Subprog FOOTER_ELEMENT(VAT,ARRAY_VAT,ARRAY_VATNET,DIMSVV,ARRAY_FOOTVAT,ARRAY_FOOTNET,DIMSVF,SAFPRIRND,WDDQUANTI,WDDNETPRI,WDSETAMOUT)
Value Char       VAT, ARRAY_VAT
Value Decimal    ARRAY_VATNET
Value Integer    DIMSVV
Value Char       ARRAY_FOOTVAT
Value Decimal    ARRAY_FOOTNET
Value Integer    DIMSVF
Value Decimal    SAFPRIRND, WDDQUANTI
Variable Decimal WDDNETPRI,WDSETAMOUT

If VAT = "" : End : Endif

Local Integer I, MODE
Local Decimal FOOT_AMT, FOOT_RAT, VAT_AMT, LIN_AMT, UN_DISC_AMT

For I = 0 To DIMSVF-1
  If ARRAY_FOOTVAT(I)<>""
    If left$(ARRAY_FOOTVAT(I),len(VAT))=VAT
      If instr(0,ARRAY_FOOTVAT(I),"~")
        MODE     = 1
        FOOT_RAT = val(seg$(ARRAY_FOOTVAT(I),instr(0,ARRAY_FOOTVAT(I),"~")+1,len(ARRAY_FOOTVAT(I))))
      Else
        MODE     = 2
        FOOT_AMT = ARRAY_FOOTNET(I)
      Endif
      Break
    Endif
  Endif
Next

If MODE = 0 : End : Endif

If MODE = 1
  WDSETAMOUT += arr(WDDNETPRI-(WDDNETPRI * (100-FOOT_RAT)/100), [L]SAFPRIRND)*WDDQUANTI
  WDDNETPRI *= arr((100-FOOT_RAT)/100, [L]SAFPRIRND)
Else
  #VAT amount
  I = find(VAT,ARRAY_VAT(0..DIMSVV-1))
  If I = 0 : End : Endif
  [L]VAT_AMT = ARRAY_VATNET(I-1)

  #Line amount
  LIN_AMT = abs(arr(([L]WDDNETPRI*[L]WDDQUANTI), [L]SAFPRIRND))

  #Unit discount amount
  If[L]VAT_AMT <> 0 & WDDQUANTI <> 0
    UN_DISC_AMT = arr(((LIN_AMT / [L]VAT_AMT)*[L]FOOT_AMT)/WDDQUANTI, [L]SAFPRIRND)
  Endif

  [L]WDDNETPRI -= UN_DISC_AMT
  #Issue X3-231356 – 2021-05-17 by VAVIL - for deliveries: we get the tag UnitPrice negative if the absolute value of a decreasing invoicing element is greater than the total amount of the lines
  If [L]VAT_AMT < [L]FOOT_AMT  : [L]WDDNETPRI = abs([L]WDDNETPRI) : Endif
  #Issue X3-231356 end
  [L]WDSETAMOUT += UN_DISC_AMT*WDDQUANTI
Endif

End
